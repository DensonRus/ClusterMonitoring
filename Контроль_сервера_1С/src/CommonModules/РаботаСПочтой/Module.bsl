// Формирует почтовый профиль по учетной записи
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты
//
// Возвращаемое значение:
//   ИнтернетПочтовыйПрофиль
//
Функция ПолучитьИнтернетПочтовыйПрофиль(УчетнаяЗапись) Экспорт

	Профиль = Новый ИнтернетПочтовыйПрофиль;
	
	Профиль.АдресСервераSMTP = УчетнаяЗапись.СерверИсходящейПочты;
	Профиль.ВремяОжидания 	 = 30;
	Профиль.Пароль           = УчетнаяЗапись.ПарольSMTP;
	Профиль.Пользователь     = УчетнаяЗапись.ПользовательSMTP;
	Профиль.ПортSMTP         = УчетнаяЗапись.ПортСервераИсходящейПочты;
	
	Если УчетнаяЗапись.СпособSMTPАутентификации <> Перечисления.СпособыSMTPАутентификации.БезАутентификации Тогда
		Профиль.ПарольSMTP         = УчетнаяЗапись.ПарольSMTP;
		Профиль.ПользовательSMTP   = УчетнаяЗапись.ПользовательSMTP;
	КонецЕсли;
	
	Если УчетнаяЗапись.СпособSMTPАутентификации = Перечисления.СпособыSMTPАутентификации.CramMD5 Тогда
		Профиль.АутентификацияSMTP = СпособSMTPАутентификации.CramMD5;
	ИначеЕсли УчетнаяЗапись.СпособSMTPАутентификации = Перечисления.СпособыSMTPАутентификации.Login Тогда
		Профиль.АутентификацияSMTP = СпособSMTPАутентификации.Login;
	ИначеЕсли УчетнаяЗапись.СпособSMTPАутентификации = Перечисления.СпособыSMTPАутентификации.Plain Тогда
		Профиль.АутентификацияSMTP = СпособSMTPАутентификации.Plain;
	ИначеЕсли УчетнаяЗапись.СпособSMTPАутентификации = Перечисления.СпособыSMTPАутентификации.БезАутентификации Тогда
		Профиль.АутентификацияSMTP = СпособSMTPАутентификации.БезАутентификации;
	Иначе
		Профиль.АутентификацияSMTP = СпособSMTPАутентификации.ПоУмолчанию;
	КонецЕсли;
	
	Возврат Профиль;

КонецФункции // ПолучитьИнтернетПочтовыйПрофиль()

// Отправляет письмо с указанными параметрами
//
// Параметры:
//	 ПочтаОбъект	- ИнтернетПочта - профиль с заполненными параметрами
//	 АдресПочты		- Строка - Адрес получателя
//	 Отправитель	- СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись от которой отправляется почта
//	 ТекстПисьма	- Строка - текст письма
//	 Тема			- Строка - тема письма
//
// Возвращаемое значение:
//   Булево - результат отправки письма
//
Функция ОтправитьПисьмо(ПочтаОбъект, АдресПочты, Отправитель, ТекстПисьма, Тема) Экспорт
		
	ЭлектронноеПисьмо       = Новый ИнтернетПочтовоеСообщение;
	ЭлектронноеПисьмо.Тема = Тема;
		
	ЭлектронноеПисьмо.Отправитель = Отправитель.АдресЭлектроннойПочты;
	ЭлектронноеПисьмо.ИмяОтправителя = Отправитель.Наименование;
	
	Текст = ЭлектронноеПисьмо.Тексты.Добавить(ТекстПисьма);
	Текст.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст;
	
	ЭлектронноеПисьмо.Получатели.Добавить(АдресПочты);
		
	Попытка
		ПочтаОбъект.Послать(ЭлектронноеПисьмо);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // ОтправитьПисьмо()